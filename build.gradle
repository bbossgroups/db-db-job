def profile = System.getProperty("profile") ?: "dev"
println(profile)
configure(allprojects) { project ->
    group = PROJ_GROUP
    version = PROJ_VERSION

    apply plugin: "java"
    apply plugin: "maven-publish"
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "signing"
    apply plugin: "java-library"
    eclipse {
        jdt {
            //if you want to alter the java versions (by default they are configured with gradle java plugin settings):
            sourceCompatibility = SOURCE_COMPILER_LEVEL
            targetCompatibility = TARGET_COMPILER_LEVEL
            //javaRuntimeName = "../../org.eclipse.jdt.launching.JRE_CONTAINER"

        }
    }

    tasks.withType(JavaCompile) {

        sourceCompatibility = SOURCE_COMPILER_LEVEL
        targetCompatibility = TARGET_COMPILER_LEVEL
        options.encoding = 'UTF-8'
    }

    tasks.withType(Javadoc) {
        sourceCompatibility = JAVADOC_SOURCE_LEVEL
        targetCompatibility = JAVADOC_COMPILER_LEVEL
        options.encoding = 'UTF-8'
        // disable the crazy super-strict doclint tool in Java 8
        // noinspection SpellCheckingInspection
        if (JavaVersion.current().isJava8Compatible()) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }

    task sourcesJar(type: Jar) {
        archiveClassifier = "sources"
        from sourceSets.main.allJava
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
    processResources {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
    task javaDocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier = "javadoc"
        from javadoc.destinationDir
    }
    artifacts {
        archives sourcesJar, javaDocJar
    }

    jar {
        manifest {
            attributes (
                    'Implementation': archiveVersion,
                    'Specification-Version': archiveVersion,
                    'Implementation-Vendor': 'bbossgroups',
                    'Implementation-ProductID': project.name,
                    'Compile-Timestamp': new Date().format('yyyy-MM-dd HH:mm:ss'),
                    'Compile-User': DEVELOPER_NAME
            )
        }
    }




    repositories {
        mavenLocal()
        maven {
            url "https://maven.aliyun.com/nexus/content/groups/public"
        }
        mavenCentral()

    }
    if(project.getProperty('skipTest').equals("true"))
    {
        compileTestJava.enabled=false
        processTestResources.enabled=false
        testClasses.enabled = false
        test.enabled = false
    }

    sourceSets {
        main {
            java {
                srcDir 'src/main/java'

            }
            if(profile == "dev") {
                resources {
                    srcDir 'src/main/resources'
                    srcDir 'src/main/java' exclude '**/*.java'
                }
            }
            else{
                resources {
                    srcDir 'src/main/resources' exclude '**/*'
                    srcDir 'src/main/java' exclude '**/*.java'
                }
            }


//			compileClasspath = configurations.api + configurations.runtime
        }
        test {
            java {
                srcDir 'src/test/java'

            }
            resources {
                srcDir 'src/test/resources'
                srcDir 'src/test/java'  exclude '**/*.java'
            }

        }

    }



    dependencies {
        testImplementation 'junit:junit:4.13.1'
        //mysql 8.0依赖
        //api group: 'mysql', name: 'mysql-connector-java', version: '8.0.33'
        //mysql 5.x依赖
        api(
                [group: 'com.mysql', name: 'mysql-connector-j', version: '9.1.0', transitive: false],
    //            [group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.17.2', transitive: true],
    //            [group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.17.2', transitive: true],
    //            [group: 'org.apache.logging.log4j', name: 'log4j-api', version: "2.17.2", transitive: true],
        )

        api(
                //[group: 'org.elasticsearch.plugin', name: 'jdbc', version: "6.3.2", transitive: true],
                [group: 'com.bbossgroups', name: 'bboss-spring-boot-starter', version: "${bboss_starter_version}", transitive: true],

        )
    //            {
    //
    //        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    //    }
        testImplementation(
                [group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: "${spring_boot_version}", transitive: true],

        )
                {
            exclude group: 'ch.qos.logback', module: 'logback-classic'
        }
        testImplementation(
                 [group: 'net.bytebuddy', name: 'byte-buddy', version: '1.7.11', transitive: true],
                [group: 'net.bytebuddy', name: 'byte-buddy-agent', version: '1.7.11', transitive: true]

        )
        api(

                [group: 'org.springframework.boot', name: 'spring-boot-autoconfigure', version: "${spring_boot_version}", transitive: true],

        ){
            exclude group: 'ch.qos.logback', module: 'logback-classic'
                }

        api(

                [group: 'org.springframework.boot', name: 'spring-boot-configuration-processor', version: "${spring_boot_version}", transitive: true]

        ){
            exclude group: 'ch.qos.logback', module: 'logback-classic'
        }

        api(
                [group: 'com.bbossgroups.plugins', name: 'bboss-datatran-jdbc', version: "${bboss_es_version}", transitive: true],
                [group: 'com.bbossgroups', name: 'bboss-schedule', version: "${bboss_version}", transitive: true],
        )
    //		api 'org.apache-extras.beanshell:bsh:2.0b6'
        api 'org.xerial:sqlite-jdbc:3.45.1.0'

        api (
                [group: 'com.bbossgroups', name: 'bboss-bootstrap-rt', version: "${bboss_version}", transitive: true],
        )



    }
    task copyJarFiles(type: Sync,dependsOn:'jar'){
        from configurations.api{
            canBeResolved=true
        }
        from jar.outputs
        exclude { details -> details.file.name.startsWith('bboss-rt')  }
        into 'build/dist/lib'

    }

    task copyRTJarFiles(type: Copy,dependsOn:'copyJarFiles'){

        from configurations.api{
            canBeResolved=true
        }
        include { details -> details.file.name.startsWith('bboss-rt')  }
        into 'build/dist'
        rename ("bboss-rt-${bboss_version}.jar", "${project.name}-${bboss_version}.jar")
    }
    task copyToolFiles(type: Copy ,dependsOn:'copyRTJarFiles')  {

        from ('runfiles')
                {
                    expand(
                            bboss_version:"${bboss_version}",
                            project:"${project.name}"
                    )

                }
        filteringCharset = 'UTF-8'
        into 'build/dist/'



    }
    task copyResourcesfiles(type: Sync )  {
        from fileTree(dir: 'src/main/resources')
        filteringCharset = 'UTF-8'
        into 'build/dist/resources'
    }

    task releaseVersion(type: Zip,dependsOn:['copyToolFiles','copyResourcesfiles']) {

        //appendix = 'wrapper'
        archiveClassifier = 'released'
        from 'build/dist/'

    }

}
